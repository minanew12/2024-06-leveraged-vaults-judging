Sour Cotton Osprey

High

# Anyone can break accounting of rewards from Convex

## Summary
Anyone can break accounting `accumulatedRewardPerVaultShare`, resulting in users receiving significantly less rewards than they should.
## Vulnerability Detail
Variable `state.accumulatedRewardPerVaultShare` is used when calculating the amount of rewards for users:
```solidity
rewardToClaim = (
            (vaultSharesBefore * rewardsPerVaultShare) / uint256(Constants.INTERNAL_TOKEN_PRECISION) 
        ) - VaultStorage.getAccountRewardDebt()[rewardToken][account]; 
```
This variable mainly depends on the number of rewards received from `CONVEX`/`AURA` and increases by the amount received when the function `claimRewardTokens()` is called:
```solidity
/// @notice Claims all the rewards for the entire vault and updates the accumulators.
function claimRewardTokens() external {
//...
        balancesBefore[i] = IERC20(state[i].rewardToken).balanceOf(address(this));
//...
        _executeClaim(rewardPool);
//...
        uint256 balanceAfter = IERC20(state[i].rewardToken).balanceOf(address(this));

        _accumulateSecondaryRewardViaClaim(
                        i,
                        state[i],
                        // balanceAfter should never be less than balanceBefore
                        balanceAfter - balancesBefore[i],
                        totalVaultSharesBefore
                    );
}
```
```solidity
function _executeClaim(RewardPoolStorage memory r) internal {
        if (r.poolType == RewardPoolType.AURA) {
            require(IAuraRewardPool(r.rewardPool).getReward(address(this), true));
        } else if (r.poolType == RewardPoolType.CONVEX_MAINNET) {
            require(IConvexRewardPool(r.rewardPool).getReward(address(this), true));
        } else if (r.poolType == RewardPoolType.CONVEX_ARBITRUM) {
            IConvexRewardPoolArbitrum(r.rewardPool).getReward(address(this));
        } else {
            revert();
        }
    }
```
```solidity
state.accumulatedRewardPerVaultShare += (
            (tokensClaimed * uint256(Constants.INTERNAL_TOKEN_PRECISION)) / totalVaultSharesBefore
        ).toUint128();
```

The problem is that `getReward()` function from Convex can be called by anyone on behalf of any address:
```solidity
function getReward(address _account, bool _claimExtras) public updateReward(_account) returns(bool){
        uint256 reward = earned(_account);
        if (reward > 0) {
            rewards[_account] = 0;
            rewardToken.safeTransfer(_account, reward);
            IDeposit(operator).rewardClaimed(pid, _account, reward);
            emit RewardPaid(_account, reward);
        }

        //also get rewards from linked rewards
        if(_claimExtras){
            for(uint i=0; i < extraRewards.length; i++){
                IRewards(extraRewards[i]).getReward(_account);
            }
        }
        return true;
    }
```
[https://docs.convexfinance.com/convexfinanceintegration/baserewardpool#claim-rewards](https://docs.convexfinance.com/convexfinanceintegration/baserewardpool#claim-rewards)

In this case rewards will be claimed, but accumulators will not be updated, since the protocol monitors the balance change only when the function `claimRewardTokens()` is called. The value of `accumulatedRewardPerVaultShare` will be much less than the actual number of rewards received.

## Impact
Malicious user can frontrun `claimRewardTokens()` by making direct calls to `BaseRewardPool.getReward()`, thus prevent updating `accumulatedRewardPerVaultShare` and resulting in users receiving significantly less rewards than they should. The remaining tokens will be stuck in the contract.
Given the high probability of the problem occurring and the impact on all users, a high severity is appropriate.
## Code Snippet
[https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/leveraged-vaults-private/contracts/vaults/common/VaultRewarderLib.sol#L146-L178](https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/common/VaultRewarderLib.sol#L146-L178)
## Tool used

Manual Review

## Recommendation
Instead of checking `balanceBefore/balanceAfter`, consider using `balanceOf(address(this))` after `_executeClaim()` to see the full amount of tokens in the contract. Or add off-chain mechanism that will track each `getReward()` call with `address(this)` as a parameter.