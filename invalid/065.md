Cool Gauze Raven

Medium

# The EthenaVault's redemptions of a user's withdrawal request with an ID=0 will always fail on Arbitrum

## Summary
For Arbitrum deployments exclusively, the `EthenaVault` will cause a **permanent DoS** for redeeming a user's position if the user's withdrawal request's `ID=0` like here...
```solidity
    /// @notice Returns the open withdraw request for a given account
    /// @return accountWithdraw an account's self initiated withdraw
    function getWithdrawRequest(address account) public view returns (WithdrawRequest memory) {
        return VaultStorage.getAccountWithdrawRequest()[account];
    }
```
... due to a hardcoded pool's address here in the `EthenaLib`...
```solidity
            exchangeData: abi.encode(CurveV2Adapter.CurveV2SingleData({
                pool: 0x167478921b907422F8E88B43C4Af2B8BEa278d3A, // <=== this pool doesn't exist on Arbitrum
                fromIndex: 1, // sUSDe
                toIndex: 0 // sDAI
            }))
        });
```
... and this will always cause a DoS to the `redeemFromNotional` function called on the `EthenaVault` contract.

### Please note that:
The protocol explicitly mentions that doesn't intend to change any hardcoded values before deploying the contracts. That can be seen on the contest's page on Sherlock.

I'm submitting this issue regarding the `EthenaVault` as: 1) it's meaningful; 2) as per discussion with Jeff Wu, when he shared his ideas about what is in-scope and what is not, I assume that because the `EthenaLib` is in-scope, same as the `BaseStakingVault` contract is, the `EthenaVault` using both `EthenaLib` and inheriting from the `BaseStakingVault` is *in-scope*, too.

## Vulnerability Detail
(https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Ethena.sol#L121-L167)

The root cause of the problem is this function in the `EthenaLib`:
```solidity
    /// @notice The vast majority of the sUSDe liquidity is in an sDAI/sUSDe curve pool.
    /// sDAI has much greater liquidity once it is unwrapped as DAI so that is done manually
    /// in this method.
    function _sellStakedUSDe(
        uint256 sUSDeAmount,
        address borrowToken,
        uint256 minPurchaseAmount,
        bytes memory exchangeData,
        uint16 dexId
    ) internal returns (uint256 borrowedCurrencyAmount) {
        Trade memory sDAITrade = Trade({
            tradeType: TradeType.EXACT_IN_SINGLE,
            sellToken: address(sUSDe),
            buyToken: address(sDAI),
            amount: sUSDeAmount,
            limit: 0, // NOTE: no slippage guard is set here, it is enforced in the second leg
                      // of the trade.
            deadline: block.timestamp,
            exchangeData: abi.encode(CurveV2Adapter.CurveV2SingleData({
                pool: 0x167478921b907422F8E88B43C4Af2B8BEa278d3A, // doesn't exist on Arbitrum
                fromIndex: 1, // sUSDe
                toIndex: 0 // sDAI
            }))
        });

        (/* */, uint256 sDAIAmount) = sDAITrade._executeTrade(uint16(DexId.CURVE_V2));
        

        // Unwraps the sDAI to DAI
        uint256 daiAmount = sDAI.redeem(sDAIAmount, address(this), address(this));
        
        if (borrowToken != address(DAI)) {
            Trade memory trade = Trade({
                tradeType: TradeType.EXACT_IN_SINGLE,
                sellToken: address(DAI),
                buyToken: borrowToken,
                amount: daiAmount,
                limit: minPurchaseAmount,
                deadline: block.timestamp,
                exchangeData: exchangeData
            });

            // Trades the unwrapped DAI back to the given token.
            (/* */, borrowedCurrencyAmount) = trade._executeTrade(dexId);
        } else {
            borrowedCurrencyAmount = daiAmount;
        }
    }
```

**Due to that** the `0x167478921b907422F8E88B43C4Af2B8BEa278d3A` Curve pool **doesn't exist on Arbitrum**, and it wasn't created with a **CREATE2** method, so it's not reproducible on the L2s anyways, **the swap will always fail**.

### Proof: https://arbiscan.io/address/0x167478921b907422F8E88B43C4Af2B8BEa278d3A#multichain-portfolio

### Another proof: the Arbitrum-based CurveV2 doesn't list the pool as available either, the hardcoded contract just has a contract size of 0, essentially. â†“
![image](https://github.com/sherlock-audit/2024-06-leveraged-vaults-c-plus-plus-equals-c-plus-one/assets/105672704/67e9b04d-b804-4b8b-9f20-df33a8883097)


### The CurveV2 swap DEX operation will always fail when executed on Arbitrum because the `pool` passed in the trade arguments doesn't exist on the Arbitrum chain....
```solidity
    /// @notice Can be used to delegate call to the TradingModule's implementation in order to execute
    /// a trade.
    function _executeTrade(
        Trade memory trade,
        uint16 dexId
    ) internal returns (uint256 amountSold, uint256 amountBought) {
        (bool success, bytes memory result) = nProxy(payable(address(Deployments.TRADING_MODULE))).getImplementation()
            .delegatecall(abi.encodeWithSelector(ITradingModule.executeTrade.selector, dexId, trade));
        if (!success) revert TradeFailed();
        (amountSold, amountBought) = abi.decode(result, (uint256, uint256));
    }
```
... and the `CurveV2Adapter` will construct incorrect data to pass to the `SwapRouter`:
```solidity
        if (trade.tradeType == TradeType.EXACT_IN_SINGLE) {
            CurveV2SingleData memory data = abi.decode(trade.exchangeData, (CurveV2SingleData));
            target = data.pool;

            executionCallData = abi.encodeWithSelector(
                ICurvePool.exchange.selector,
                data.fromIndex, data.toIndex,
                trade.amount,
                trade.limit
            );
        } 
```

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/trading/adapters/CurveV2Adapter.sol#L56

### An instant redemption case calls the swap (`_sellStakedUSDe`) function here in the `EthenaVault` contract:
```solidity
    function _executeInstantRedemption(
        address /* account */,
        uint256 vaultShares,
        uint256 /* maturity */,
        RedeemParams memory params
    ) internal override returns (uint256 borrowedCurrencyAmount) {
        uint256 sUSDeToSell = getStakingTokensForVaultShare(vaultShares);

        // Selling sUSDe requires special handling since most of the liquidity <=========
        // sits inside a sUSDe/sDAI pool on Curve. <=========
        return EthenaLib._sellStakedUSDe(
            sUSDeToSell, BORROW_TOKEN, params.minPurchaseAmount, params.exchangeData, params.dexId
        );
    }
```

And the instant redemption is triggered here in the `BaseStakingVault`, a contract that is inherited by the `EthenaVault:
https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/BaseStakingVault.sol#L152
```solidity
        if (accountWithdraw.requestId == 0) {
            return _executeInstantRedemption(account, vaultShares, maturity, params);
        }
```

## Impact
As per the Sherlock classification, the severity is *Medium*, as it causes a DoS to certain parts of the protocol.

Here's the cause of the issue in the `BaseStakingVault` contract:
```solidity
    /// @notice Called when an account exits from the vault.
    function _redeemFromNotional(
        address account,
        uint256 vaultShares,
        uint256 maturity,
        bytes calldata data
    ) internal override returns (uint256 borrowedCurrencyAmount) {
        // Short circuit here to allow for direct repayment of debts. This method always
        // gets called by Notional on every exit, but in times of illiquidity an account
        // may want to pay down their debt without being able to instantly redeem their
        // vault shares to avoid liquidation.
        if (vaultShares == 0) return 0;

        WithdrawRequest memory accountWithdraw = getWithdrawRequest(account);

        RedeemParams memory params = abi.decode(data, (RedeemParams));
        if (accountWithdraw.requestId == 0) {
            return _executeInstantRedemption(account, vaultShares, maturity, params); // <========= here this will always revert for the EthenaVault on Arbitrum
        } else {
        //  (
        // ...
        }
     }
```

The particular function that will be DoS'ed for the end users is this endpoint:
```solidity
    function redeemFromNotional(
        address account,
        address receiver,
        uint256 vaultShares,
        uint256 maturity,
        uint256 underlyingToRepayDebt,
        bytes calldata data
    ) external onlyNotional returns (uint256 transferToReceiver) {
        uint256 borrowedCurrencyAmount = _redeemFromNotional(account, vaultShares, maturity, data);

        uint256 transferToNotional;
        if (account == address(this) || borrowedCurrencyAmount <= underlyingToRepayDebt) {
            // It may be the case that insufficient tokens were redeemed to repay the debt. If this
            // happens the Notional will attempt to recover the shortfall from the account directly.
            // This can happen if an account wants to reduce their leverage by paying off debt but
            // does not want to sell strategy tokens to do so.
            // The other situation would be that the vault is calling redemption to deleverage or
            // settle. In that case all tokens go back to Notional.
            transferToNotional = borrowedCurrencyAmount;
        } else {
            transferToNotional = underlyingToRepayDebt;
            unchecked { transferToReceiver = borrowedCurrencyAmount - underlyingToRepayDebt; }
        }

        if (_UNDERLYING_IS_ETH) {
            if (transferToReceiver > 0) payable(receiver).transfer(transferToReceiver);
            if (transferToNotional > 0) payable(address(NOTIONAL)).transfer(transferToNotional);
        } else {
            if (transferToReceiver > 0) _UNDERLYING_TOKEN.checkTransfer(receiver, transferToReceiver);
            if (transferToNotional > 0) _UNDERLYING_TOKEN.checkTransfer(address(NOTIONAL), transferToNotional);
        }
    }
```

(https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/BaseStakingVault.sol#L151C1-L153C10)


## Code Snippet
https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Ethena.sol#L140

## Tool used
Manual review.

## Recommendation
Consider switching the pool for the Arbitrum deployments, or, alternatively, make it depend on the current `chainid()`: leave it `0x167478921b907422F8E88B43C4Af2B8BEa278d3A` for Ethereum, but choose a different pool for Arbitrum.

## Other notes
According to what is also mentioned on the contest's page, the protocol initially assumes that the hardcoded sUSDe:sDAI swap pool should work on Arbitrum, at least for now, at the time of the initial deployments:

> Please list any known issues and explicitly state the acceptable risks for each known issue.
> 
> The Ethena library (contracts/vaults/staking/protocols/Ethena.sol) has a hardcoded trade of sUSDe to sDAI on a specific Curve pool. This is due to issues handling the withdraw of sDAI to DAI inside the trading module. While this trade works right now, in the future the liquidity for sUSDe may move to a different trading venue and any affected contracts would need to be upgraded.