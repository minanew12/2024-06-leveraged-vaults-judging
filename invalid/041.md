Hidden Laurel Cod

High

# In the _splitWithdrawRequest() function, there exists an issue that causes both the from and to requestId to be 0


## Summary
In the _splitWithdrawRequest() function, there exists an issue that causes both the from and to requestId to be 0
## Vulnerability Detail
```javascript
 function _splitWithdrawRequest(address _from, address _to, uint256 vaultShares) internal {
 @>       WithdrawRequest storage w = VaultStorage.getAccountWithdrawRequest()[_from];
        if (w.requestId == 0) return;

        // Create a new split withdraw request
        if (!w.hasSplit) {
            SplitWithdrawRequest memory s = VaultStorage.getSplitWithdrawRequest()[w.requestId];
            // Safety check to ensure that the split withdraw request is not active, split withdraw
            // requests are never deleted. This presumes that all withdraw request ids are unique.
            require(s.finalized == false && s.totalVaultShares == 0);
            VaultStorage.getSplitWithdrawRequest()[w.requestId].totalVaultShares = w.vaultShares;
        }

        if (w.vaultShares == vaultShares) {
            // If the resulting vault shares is zero, then delete the request. The _from account's
            // withdraw request is fully transferred to _to
    @>        delete VaultStorage.getAccountWithdrawRequest()[_from];
        } else {
            // Otherwise deduct the vault shares
            w.vaultShares = w.vaultShares - vaultShares;
            w.hasSplit = true;
        }

        // Ensure that no withdraw request gets overridden, the _to account always receives their withdraw
        // request in the account withdraw slot.
        WithdrawRequest storage toWithdraw = VaultStorage.getAccountWithdrawRequest()[_to];
        require(toWithdraw.requestId == 0 || toWithdraw.requestId == w.requestId , "Existing Request");

        // Either the request gets set or it gets incremented here.
@>      toWithdraw.requestId = w.requestId;
        toWithdraw.vaultShares = toWithdraw.vaultShares + vaultShares;
        toWithdraw.hasSplit = true;
    }
```
The ‘w’ is the storage WithdrawRequest of _from. In the case of w.vaultShares equaling vaultShares, the storage WithdrawRequest of _from is deleted (meaning the storage is reset to 0). Then w.requestId becomes 0, so toWithdraw.requestId is 0.
Funds cannot be retrieved from third-party protocols, leading to a loss of funds.

## Impact
Funds cannot be retrieved from third-party protocols, leading to a loss of funds.
## Code Snippet
https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/14d3eaf0445c251c52c86ce88a84a3f5b9dfad94/leveraged-vaults-private/contracts/vaults/common/WithdrawRequestBase.sol#L205
## Tool used

Manual Review

## Recommendation
Please use memory cached variables.
