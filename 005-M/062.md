Tart Aegean Nightingale

High

# Malicious withdrawal requests can be injected into the holder's queue

## Summary

Malicious users can intentionally inject a specially crafted withdrawal request into the LIDO's withdrawal queue of cooldown holders, resulting in a loss of assets or unexpected liquidation.

## Vulnerability Detail

When a vault account initiates a withdrawal, the `_initiateWithdrawImpl` function below will be executed. Within this function, a new `KelpCooldownHolder` holder contract will be created. Then, the rsETH will be transferred to the newly created holder, and the holder will initiate the withdrawal from Kelp. Assume that the address of newly created holder is `0x123456`.

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Kelp.sol#L140

```solidity
File: Kelp.sol
140:     function _initiateWithdrawImpl(
141:         uint256 balanceToTransfer,
142:         address holderImplementation
143:     ) internal returns (uint256 requestId) {
144:         KelpCooldownHolder holder = KelpCooldownHolder(payable(Clones.clone(holderImplementation)));
145:         rsETH.transfer(address(holder), balanceToTransfer);
146:         holder.startCooldown();
147: 
148:         return uint256(uint160(address(holder)));
149:     }
```

When the withdrawal is finalized, the `triggerExtraStep` needs to be called to start withdrawal process from Lido so we can unwrap stETH to ETH. At Line 83 below, it will call the `LidoWithdraw.requestWithdrawals` function, and its request will be added to the LIDO's withdrawal queue.

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Kelp.sol#L82

```solidity
File: Kelp.sol
69:     /// @notice this method need to be called once withdraw on Kelp is finalized
70:     /// to start withdraw process from Lido so we can unwrap stETH to ETH
71:     /// since we are not able to withdraw ETH directly from Kelp
72:     function triggerExtraStep() external {
73:         require(!triggered);
74:         (/* */, /* */, /* */, uint256 userWithdrawalRequestNonce) = WithdrawManager.getUserWithdrawalRequest(stETH, address(this), 0);
75:         require(userWithdrawalRequestNonce < WithdrawManager.nextLockedNonce(stETH));
76: 
77:         WithdrawManager.completeWithdrawal(stETH);
78:         uint256 tokensClaimed = IERC20(stETH).balanceOf(address(this));
79: 
80:         uint256[] memory amounts = new uint256[](1);
81:         amounts[0] = tokensClaimed;
82:         IERC20(stETH).approve(address(LidoWithdraw), amounts[0]);
83:         LidoWithdraw.requestWithdrawals(amounts, address(this));
84: 
85:         triggered = true;
86:     }
```

An important to note here is that anyone calls the LIDO's `requestWithdrawals` and set to `_owner` parameter to an arbitrary address. Assume that right before the `triggerExtraStep` function is executed, a malicious user calls LIDO's `requestWithdrawals` with the smallest possible stETH amount (e.g., 1 wei) and sets the `_owner` to  `0x123456`.

After the malicious user's transaction is executed, the `triggerExtraStep` transaction will be executed to withdraw 10 stETH.

https://etherscan.io/address/0xe42c659dc09109566720ea8b2de186c2be7d94d9#code#F22#L125

```solidity
/// @notice Request the batch of stETH for withdrawal. Approvals for the passed amounts should be done before.
/// @param _amounts an array of stETH amount values.
///  The standalone withdrawal request will be created for each item in the passed list.
/// @param _owner address that will be able to manage the created requests.
///  If `address(0)` is passed, `msg.sender` will be used as owner.
/// @return requestIds an array of the created withdrawal request ids
function requestWithdrawals(uint256[] calldata _amounts, address _owner)
    public
    returns (uint256[] memory requestIds)
{
    _checkResumed();
    if (_owner == address(0)) _owner = msg.sender;
    requestIds = new uint256[](_amounts.length);
    for (uint256 i = 0; i < _amounts.length; ++i) {
        _checkWithdrawalRequestAmount(_amounts[i]);
        requestIds[i] = _requestWithdrawal(_amounts[i], _owner);
    }
}
```

When the LIDO's `getWithdrawalRequests` function, it will return an array with two items `[reqID-Attacker-1wei, reqID-TriggerExtra-10stETH]`.

https://etherscan.io/address/0xe42c659dc09109566720ea8b2de186c2be7d94d9#code#F22#L201

```solidity
/// @notice Returns all withdrawal requests that belongs to the `_owner` address
function getWithdrawalRequests(address _owner) external view returns (uint256[] memory requestsIds) {
    return _getRequestsByOwner()[_owner].values();
}
```

From this point onwards, there are a number of attacks that can be performed. Following are some of the negative impact or attack that can be performed due to this issue:

#### Instance 1 - Causing assets to be lost

Since Line 100 only claim the first request ID, which is attacker-1wei, the second requestID will be ignored and lost.

The claimedToken will be set to 1wei instead of 100stETH.

When finalizing the cooldown or withdrawal, Line 100 will only call `LidoWithdraw.claimWithdrawal` function against the first item (`reqID-Attacker-1wei`) in the array and proceed to claim the attacker's withdrawal request while ignoring the actual withdrawal request in the second position of the array. Thus, the funds (entire 10 stETH) in the second/actual withdrawal request will be lost.

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Kelp.sol#L100

```solidity
File: Kelp.sol
088:     function _finalizeCooldown() internal override returns (uint256 tokensClaimed, bool finalized) {
089:         if (!triggered) {
090:             return (0, false);
091:         }
092: 
093:         uint256[] memory requestIds = LidoWithdraw.getWithdrawalRequests(address(this));
094:         ILidoWithdraw.WithdrawalRequestStatus[] memory withdrawsStatus = LidoWithdraw.getWithdrawalStatus(requestIds);
095: 
096:         if (!withdrawsStatus[0].isFinalized) {
097:             return (0, false);
098:         }
099:
100:         LidoWithdraw.claimWithdrawal(requestIds[0]);
101: 
102:         tokensClaimed = address(this).balance;
103:         (bool sent,) = vault.call{value: tokensClaimed}("");
104:         require(sent);
105:         finalized = true;
106:     }
```

#### Instance 2 - Causing the account's collateral to be undervalued, leading to unexpected liquidation

When the `_getValueOfWithdrawRequest` is called to compute an account's collateral value, the `expectedStETHAmount` at Line 126 below will be 1 wei instead of 10 stETH. This significantly undervalues the vault account's collateral, leading to the liquidation of the affected account.

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Kelp.sol#L126

```solidity
File: Kelp.sol
114:     function _getValueOfWithdrawRequest(
115:         WithdrawRequest memory w,
116:         address borrowToken,
117:         uint256 borrowPrecision
118:     ) internal view returns (uint256) {
119:         address holder = address(uint160(w.requestId));
120: 
121:         uint256 expectedStETHAmount;
122:         if (KelpCooldownHolder(payable(holder)).triggered()) {
123:             uint256[] memory requestIds = LidoWithdraw.getWithdrawalRequests(holder);
124:             ILidoWithdraw.WithdrawalRequestStatus[] memory withdrawsStatus = LidoWithdraw.getWithdrawalStatus(requestIds);
125: 
126:             expectedStETHAmount = withdrawsStatus[0].amountOfStETH;
127:         } else {
128:             (/* */, expectedStETHAmount, /* */, /* */) = WithdrawManager.getUserWithdrawalRequest(stETH, holder, 0);
129: 
130:         }
```

## Impact

The issue leads to the following:

- Loss of assets
- Account's collateral to be undervalued, leading to unexpected liquidation

## Code Snippet

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Kelp.sol#L140

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Kelp.sol#L82

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Kelp.sol#L100

https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/protocols/Kelp.sol#L126

## Tool used

Manual Review

## Recommendation

Consider the following changes to mitigate the issue:

At Line 83, `LidoWithdraw.requestWithdrawals` will return the requestID of the withdrawal request. Save the returned requestID in the storage.

```solidity
File: Kelp.sol
72:     function triggerExtraStep() external {
73:         require(!triggered);
74:         (/* */, /* */, /* */, uint256 userWithdrawalRequestNonce) = WithdrawManager.getUserWithdrawalRequest(stETH, address(this), 0);
75:         require(userWithdrawalRequestNonce < WithdrawManager.nextLockedNonce(stETH));
76: 
77:         WithdrawManager.completeWithdrawal(stETH);
78:         uint256 tokensClaimed = IERC20(stETH).balanceOf(address(this));
79: 
80:         uint256[] memory amounts = new uint256[](1);
81:         amounts[0] = tokensClaimed;
82:         IERC20(stETH).approve(address(LidoWithdraw), amounts[0]);
83:         LidoWithdraw.requestWithdrawals(amounts, address(this));
84: 
85:         triggered = true;
86:     }
```

#### Fix for Instance 1

At Line 100 below, do not use the `requestIds[0])`. Instead, use the request ID that was saved earlier. In this case, it will ignore any withdraw requests that are not initialized by the holder.

```solidity
088:     function _finalizeCooldown() internal override returns (uint256 tokensClaimed, bool finalized) {
089:         if (!triggered) {
090:             return (0, false);
091:         }
092: 
093:         uint256[] memory requestIds = LidoWithdraw.getWithdrawalRequests(address(this));
094:         ILidoWithdraw.WithdrawalRequestStatus[] memory withdrawsStatus = LidoWithdraw.getWithdrawalStatus(requestIds);
095: 
096:         if (!withdrawsStatus[0].isFinalized) {
097:             return (0, false);
098:         }
099:
100:         LidoWithdraw.claimWithdrawal(requestIds[0]);
101: 
102:         tokensClaimed = address(this).balance;
103:         (bool sent,) = vault.call{value: tokensClaimed}("");
104:         require(sent);
105:         finalized = true;
106:     }
```

#### Fix for Instance 2

At Line 126 below, do not use the `withdrawsStatus[0])`. Instead, use the request ID that was saved earlier. In this case, it will ignore any withdraw requests that are not initialized by the holder.

```solidity
File: Kelp.sol
114:     function _getValueOfWithdrawRequest(
115:         WithdrawRequest memory w,
116:         address borrowToken,
117:         uint256 borrowPrecision
118:     ) internal view returns (uint256) {
119:         address holder = address(uint160(w.requestId));
120: 
121:         uint256 expectedStETHAmount;
122:         if (KelpCooldownHolder(payable(holder)).triggered()) {
123:             uint256[] memory requestIds = LidoWithdraw.getWithdrawalRequests(holder);
124:             ILidoWithdraw.WithdrawalRequestStatus[] memory withdrawsStatus = LidoWithdraw.getWithdrawalStatus(requestIds);
125: 
126:             expectedStETHAmount = withdrawsStatus[0].amountOfStETH;
127:         } else {
128:             (/* */, expectedStETHAmount, /* */, /* */) = WithdrawManager.getUserWithdrawalRequest(stETH, holder, 0);
129: 
130:         }
```

#### Fix for the rest of the codebase

Line 165 below also depends on the `withdrawsStatus[0]`. Thus, it also need to be replace with the request ID that was saved earlier.

```solidity
File: Kelp.sol
158:     function _canFinalizeWithdrawRequest(uint256 requestId) internal view returns (bool) {
159:         address holder = address(uint160(requestId));
160:         if (!KelpCooldownHolder(payable(holder)).triggered()) return false;
161: 
162:         uint256[] memory requestIds = LidoWithdraw.getWithdrawalRequests(holder);
163:         ILidoWithdraw.WithdrawalRequestStatus[] memory withdrawsStatus = LidoWithdraw.getWithdrawalStatus(requestIds);
164: 
165:         return withdrawsStatus[0].isFinalized;
166:     }
```