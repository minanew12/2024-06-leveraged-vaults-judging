Shiny Amber Elephant

Medium

# WithdrawRequestBase::_splitWithdrawRequest limiting 1 split per user can lead to liquidations being delayed

## Summary

When an account has a poor collateral ratio it can be liquidated by calling `BaseStakingVault::deleverageAccount()` which further calls `WithdrawRequestBase::_splitWithdrawRequest()` if the liquidated account has a withdrawal request. The issue is that each user/ liquidator can only be assigned one split, which will block a liquidator (could be a liquidation bot or a normal MEV user) from being able to perform more liquidations until their split is finalized (which can be very soon or multiple days/ weeks). 

## Vulnerability Detail
[BaseStakingVault::deleverageAccount()](https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/BaseStakingVault.sol#L208-L237)
```solidity
    function deleverageAccount(
        address account,
        address vault,
        address liquidator,
        uint16 currencyIndex,
        int256 depositUnderlyingInternal
    ) external payable override virtual returns (
        uint256 vaultSharesFromLiquidation,
        int256 depositAmountPrimeCash
    ) {
        require(msg.sender == liquidator);
        _checkReentrancyContext();

        // Do not allow liquidations if the result will be that the account is insolvent. This may occur if the
        // short term de-peg of an asset causes a bad debt to accrue to the protocol. In this case, we should be
        // able to execute a forced withdraw request and wait for a full return on the staked token.
        (VaultAccountHealthFactors memory healthBefore, /* */, /* */) = NOTIONAL.getVaultAccountHealthFactors(
            account, vault
        );
        require(0 <= healthBefore.collateralRatio, "Insolvent");

        // Executes the liquidation on Notional, vault shares are transferred from the account to the liquidator
        // inside this process.
        (vaultSharesFromLiquidation, depositAmountPrimeCash) = NOTIONAL.deleverageAccount{value: msg.value}(
            account, vault, liquidator, currencyIndex, depositUnderlyingInternal
        );

        // Splits any withdraw requests, if required. Will revert if the liquidator cannot absorb the withdraw
        // request because they have another active one.
>>      _splitWithdrawRequest(account, liquidator, vaultSharesFromLiquidation);
```
When an account is being deleveraged, if it contains a withdrawal request then that request will be split and assigned to the liquidator (for the protion that is being liquidated). 

[WithdrawRequestBase::_splitWithdrawRequest()](https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/common/WithdrawRequestBase.sol#L205-L237)
```solidity
    function _splitWithdrawRequest(address _from, address _to, uint256 vaultShares) internal {
        WithdrawRequest storage w = VaultStorage.getAccountWithdrawRequest()[_from];
        if (w.requestId == 0) return;

        // Create a new split withdraw request
        if (!w.hasSplit) {
            SplitWithdrawRequest memory s = VaultStorage.getSplitWithdrawRequest()[w.requestId];
            // Safety check to ensure that the split withdraw request is not active, split withdraw
            // requests are never deleted. This presumes that all withdraw request ids are unique.
            require(s.finalized == false && s.totalVaultShares == 0);
            VaultStorage.getSplitWithdrawRequest()[w.requestId].totalVaultShares = w.vaultShares;
        }

        if (w.vaultShares == vaultShares) {
            // If the resulting vault shares is zero, then delete the request. The _from account's
            // withdraw request is fully transferred to _to
            delete VaultStorage.getAccountWithdrawRequest()[_from];
        } else {
            // Otherwise deduct the vault shares
            w.vaultShares = w.vaultShares - vaultShares;
            w.hasSplit = true;
        }

        // Ensure that no withdraw request gets overridden, the _to account always receives their withdraw
        // request in the account withdraw slot.
        WithdrawRequest storage toWithdraw = VaultStorage.getAccountWithdrawRequest()[_to];
>>      require(toWithdraw.requestId == 0 || toWithdraw.requestId == w.requestId , "Existing Request");

        // Either the request gets set or it gets incremented here.
        toWithdraw.requestId = w.requestId;
        toWithdraw.vaultShares = toWithdraw.vaultShares + vaultShares;
        toWithdraw.hasSplit = true;
    }
```
The limitation of each liquidator only having 1 split request at a time can lead to liquidations being blocked, which can lead to accounts reaching insolvency to have their liquidation delayed (due to less liquidators being available) if there has been a recent market crash. 

The main issue is that an account cannot immediately finalize their liquidated split that was created during a liquidation, as there is a withdrawal waiting period (which can be instant if lucky, or take weeks). This means that the limitation of 1 split per liquidator can cause a backlog of liquidations between liquidators, limiting the number of liquidators that can liquidate accounts. This can allow accounts to fall into insolvency due to inadequate liquidator availablity, putting the protocol and users at risk.

The protocol has stated they will be using liquidation bots, which I assume will also be involved in calling `deleverageAccount`, however these bots will also face this same issue with their call failing if they already have a split, which will also block them from performing liquidations on accounts that have withdrawalRequests.

## Impact

Liquidations can be blocked or delayed due to the limitation of 1 split per account (liquidator address), both for regular liquidators and liquidations bots, which can lead to accounts becoming insolvent due to these blocked liquidations. It cannot be assumed that there will be infinite liquidation bots and user who perform liquidations, therefore this is a scenario that needs to be considered for protocol safety.

## Code Snippet

[BaseStakingVault::deleverageAccount()](https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/staking/BaseStakingVault.sol#L208-L237)
[WithdrawRequestBase::_splitWithdrawRequest()](https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/main/leveraged-vaults-private/contracts/vaults/common/WithdrawRequestBase.sol#L205-L237)

## Tool used

Manual Review

## Recommendation

Allow liquidators to have more than 1 split from liquidations, this would require a rewrite of the split mechanism using different split Ids.
