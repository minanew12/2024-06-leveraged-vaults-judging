Modern Amber Blackbird

Medium

# EthenaLib#_sellStakedUSDe() fails to enforce the slippage control using ```minPurchaseAmount```  when borrowToken is DAI. A user might receive less borrowToken than expected.

## Summary
EthenaLib#_sellStakedUSDe() fails to enforce the slippage control using ```minPurchaseAmount```  when borrowToken is DAI.

## Vulnerability Detail

EthenaLib#_sellStakedUSDe() allows one to trade sUSDe to borrowTokens.  It uses ```minPurchaseAmount``` for slippage control. That means, the amount of output token must be no less than  ```minPurchaseAmount```.

[https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/14d3eaf0445c251c52c86ce88a84a3f5b9dfad94/leveraged-vaults-private/contracts/vaults/staking/protocols/Ethena.sol#L124-L167](https://github.com/sherlock-audit/2024-06-leveraged-vaults/blob/14d3eaf0445c251c52c86ce88a84a3f5b9dfad94/leveraged-vaults-private/contracts/vaults/staking/protocols/Ethena.sol#L124-L167)

The implementation, however, does not ensure the slippage control. It relies one the second leg to does such slippage control. However, when ``borrowToken == DAI``, there is no second leg to trade DAI for borrowToken. This means, the amount of DAI will not be checked against ```minPurchaseAmount```. As a result, only the folloiwng code is executed: 

```javascript
        } else {
            borrowedCurrencyAmount = daiAmount;
        }
```
It is possible that both ```borrowedCurrencyAmount``` and  ```daiAmount``` might be smaller than  ```minPurchaseAmount```. So the slippage control fails here. 

## Impact
EthenaLib#_sellStakedUSDe() fails to enforce the slippage control using ```minPurchaseAmount```  when borrowToken is DAI. A user might receive less borrowToken than expected. 

## Code Snippet

## Tool used
Foundry

Manual Review

## Recommendation
Make sure the amount of DAI is not less than  ```minPurchaseAmount``` when the borrowToken is DAI:

```diff
 function _sellStakedUSDe(
        uint256 sUSDeAmount,
        address borrowToken,
        uint256 minPurchaseAmount,
        bytes memory exchangeData,
        uint16 dexId
    ) internal returns (uint256 borrowedCurrencyAmount) {
        Trade memory sDAITrade = Trade({
            tradeType: TradeType.EXACT_IN_SINGLE,
            sellToken: address(sUSDe),
            buyToken: address(sDAI),
            amount: sUSDeAmount,
            limit: 0, // NOTE: no slippage guard is set here, it is enforced in the second leg
                      // of the trade.
            deadline: block.timestamp,
            exchangeData: abi.encode(CurveV2Adapter.CurveV2SingleData({
                pool: 0x167478921b907422F8E88B43C4Af2B8BEa278d3A,
                fromIndex: 1, // sUSDe
                toIndex: 0 // sDAI
            }))
        });

        (/* */, uint256 sDAIAmount) = sDAITrade._executeTrade(uint16(DexId.CURVE_V2));

        // Unwraps the sDAI to DAI
        uint256 daiAmount = sDAI.redeem(sDAIAmount, address(this), address(this));
        
        if (borrowToken != address(DAI)) {
            Trade memory trade = Trade({
                tradeType: TradeType.EXACT_IN_SINGLE,
                sellToken: address(DAI),
                buyToken: borrowToken,
                amount: daiAmount,
                limit: minPurchaseAmount,
                deadline: block.timestamp,
                exchangeData: exchangeData
            });

            // Trades the unwrapped DAI back to the given token.
            (/* */, borrowedCurrencyAmount) = trade._executeTrade(dexId);
        } else {
            borrowedCurrencyAmount = daiAmount;
+            require(borrowedCurrencyAmount >= minPurchaseAmount, 'slippage control fails");
        }
    }
```
